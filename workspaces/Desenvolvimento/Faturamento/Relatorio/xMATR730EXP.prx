#INCLUDE "PROTHEUS.CH"
#INCLUDE "TOTVS.ch"
#INCLUDE "prtopdef.ch"
//----------------------------------------------------------------------------------------------------------------------
/* {Protheus.doc} PEDIDOS POR CLIENTES
RELATORIO - PEDIDOS POR CLIENTES
@author    BRUNO NASCIMENTO GONÇALVES
@since     25/09/2023
@version   12/superior
*/
//----------------------------------------------------------------------------------------------------------------------

USER FUNCTION xMATR730EXP()

    LOCAL oReport := NIL
    LOCAL aPergs  := {}
    LOCAL aResps  := {}
    LOCAL cQuery3 := ""

    cQuery3 := " SELECT A.[USR_DEPTO] " + CRLF
    cQuery3 += " FROM [SYS_USR] A" + CRLF
    cQuery3 += " WHERE A.[USR_ID] = '"+  __CUSERID +"'"

    cAliasUS := MPSYSOPENQUERY(cQuery3)

    IF ALLTRIM((cAliasUS)->USR_DEPTO) == "EXPEDICAO"
        AADD(aPergs, {2, "QUAL STATUS DA IMPRESSÃO", " ", {" ","AGUARDANDO IMPRESSÃO"},100,"",.F.})
        AADD(aPergs, {1, "PEDIDO DE", SPACE(TAMSX3("C5_NUM")[1]) ,,,"SC5",, 100, .F.})
        AADD(aPergs, {1, "PEDIDO ATE", SPACE(TAMSX3("C5_NUM")[1]) ,,,"SC5",, 100, .F.})
    ELSEIF ALLTRIM((cAliasUS)->USR_DEPTO) <> "EXPEDICAO"
        AADD(aPergs, {1, "PEDIDO DE", SPACE(TAMSX3("C5_NUM")[1]) ,,,"SC5",, 100, .F.})
        AADD(aPergs, {1, "PEDIDO ATE", SPACE(TAMSX3("C5_NUM")[1]) ,,,"SC5",, 100, .F.})    
    ENDIF

    IF PARAMBOX(aPergs, "Parametros do relatorio", @aResps,,,,,,,, .T., .T.)
        oReport := REPORTDEF(aResps)
        oReport:PRINTDIALOG()
    ENDIF

RETURN

STATIC FUNCTION REPORTDEF(aResps)

    LOCAL oReport      := NIL
    LOCAL oSection1    := NIL
    LOCAL oSection2    := NIL
    LOCAL oSection3    := NIL
    LOCAL nColSpace    := 0
    LOCAL nSize        := 25
    LOCAL nSize1       := 255
    LOCAL lLineBreak   := .T.
    LOCAL lAutoSize    := .T.
    LOCAL cAlign       := "LEFT"
    LOCAL cHeaderAlign := "LEFT"
    LOCAL cAliasCL     := ""
    LOCAL cAliasPD     := ""
    LOCAL cAliasOB     := "" 
    LOCAL cNomeArq     := "PEDIDO POR CLIENTES"
    LOCAL cTitulo      := "PEDIDO POR CLIENTES"
    PUBLIC OBS

    oReport := TREPORT():NEW(cNomeArq, cTitulo, "", {|oReport| REPORTPRINT(oReport, @cAliasCL, @cAliasPD, @cAliasOB, aResps)}, "IMPRESSÃO DE RELATORIO")

    oSection1 := TRSECTION():NEW(oReport)
    TRCELL():NEW(oSection1, "A1_COD",      cAliasCL,  "COD.CLI"       ,,nSize,,{|| (cAliasCL)->A1_COD},cAlign,lLineBreak,cHeaderAlign,,nColSpace,lAutoSize)
    TRCELL():NEW(oSection1, "A1_NREDUZ",   cAliasCL,  "CLIENTE"       ,,nSize,,{|| (cAliasCL)->A1_NREDUZ},cAlign,lLineBreak,cHeaderAlign,,nColSpace,lAutoSize)
    TRCELL():NEW(oSection1, "A1_LOJA",     cAliasCL,  "LOJA"          ,,nSize,,{|| (cAliasCL)->A1_LOJA},cAlign,lLineBreak,cHeaderAlign,,nColSpace,lAutoSize)
    TRCELL():NEW(oSection1, "A1_END",      cAliasCL,  "ENDERECO"      ,,nSize,,{|| (cAliasCL)->A1_END},cAlign,lLineBreak,cHeaderAlign,,nColSpace,lAutoSize) 
    TRCELL():NEW(oSection1, "C5_NUM",      cAliasCL,  "N PEDIDO"      ,,nSize,,{|| (cAliasCL)->C5_NUM},cAlign,lLineBreak,cHeaderAlign,,nColSpace,lAutoSize)
    TRCELL():NEW(oSection1, "C5_EMISSAO",  cAliasCL,  "EMISSAO"       ,,nSize,,{|| (cAliasCL)->C5_EMISSAO},cAlign,lLineBreak,cHeaderAlign,,nColSpace,lAutoSize)
    TRCELL():NEW(oSection1, "E4_DESCRI",   cAliasCL,  "COND.PAG"      ,,nSize,,{|| (cAliasCL)->E4_DESCRI},cAlign,lLineBreak,cHeaderAlign,,nColSpace,lAutoSize)
    TRCELL():NEW(oSection1, "C5_PEDMER",   cAliasCL,  "PED.MERCANET"  ,,nSize,,{|| (cAliasCL)->C5_PEDMER},cAlign,lLineBreak,cHeaderAlign,,nColSpace,lAutoSize)
    TRCELL():NEW(oSection1, "A3_NOME",     cAliasCL,  "REPRESENTANTE" ,,nSize,,{|| (cAliasCL)->A3_NOME},cAlign,lLineBreak,cHeaderAlign,,nColSpace,lAutoSize)

    oSection2 := TRSECTION():NEW(oReport)
    TRCELL():NEW(oSection2, "EMBAL",      cAliasPD, "EMBAL"             ,,nSize,,{|| (cAliasPD)->EMBAL},cAlign,,cHeaderAlign,,nColSpace)
    TRCELL():NEW(oSection2, "C6_PRODUTO", cAliasPD, "ITEM"              ,,nSize,,{|| (cAliasPD)->C6_PRODUTO},cAlign,,cHeaderAlign,,nColSpace)
    TRCELL():NEW(oSection2, "C6_QTDVEN",  cAliasPD, "QTDE"              ,,nSize,,{|| (cAliasPD)->C6_QTDVEN},cAlign,,cHeaderAlign,,nColSpace)
    TRCELL():NEW(oSection2, "C6_DESCRI",  cAliasPD, "DESCRIÇÃO DO ITEM" ,,nSize1,,{|| (cAliasPD)->C6_DESCRI},cAlign,,cHeaderAlign,,nColSpace)
    TRCELL():NEW(oSection2, "C6_PRCVEN",  cAliasPD, "PR.UNIT"           ,,nSize,,{|| (cAliasPD)->C6_PRCVEN},cAlign,,cHeaderAlign,,nColSpace)
    TRCELL():NEW(oSection2, "C6_VALOR",   cAliasPD, "VL.PED"            ,,nSize,,{|| (cAliasPD)->C6_VALOR},cAlign,,cHeaderAlign,,nColSpace)
    TRCELL():NEW(oSection2, "VALOR_EXP",  cAliasPD, "VL.Entrega"        ,,nSize,,{|| (cAliasPD)->VALOR_EXP},cAlign,,cHeaderAlign,,nColSpace)
    TRCELL():NEW(oSection2, "C6_ENDPAD",  cAliasPD, "DOCA"              ,,nSize,,{|| (cAliasPD)->C6_ENDPAD},cAlign,,cHeaderAlign,,nColSpace)

    oSection3 := TRSECTION():NEW(oReport)   
    TRCELL():NEW(oSection3, "C5_XOBS", cAliasOB, "OBSERVAÇÃO DO PEDIDO",,,,{|| ALLTRIM((cAliasOB)->C5_XOBS)},cAlign,lLineBreak,cHeaderAlign,,nColSpace,lAutoSize)

    TRFUNCTION():NEW(oSection2:CELL("C6_VALOR"),,"SUM",,,"@E 9,999,999,999.99",,.T.,.F.,,oSection2)
    TRFUNCTION():NEW(oSection2:CELL("VALOR_EXP"),,"SUM",,,"@E 9,999,999,999.99",,.T.,.F.,,oSection2)
    TRFUNCTION():NEW(oSection2:CELL("EMBAL"),,"SUM",,,"@E 9,999,999,999.99",,.T.,.F.,,oSection2)

RETURN oReport

STATIC FUNCTION REPORTPRINT(oReport, cAliasCL, cAliasPD, cAliasOB, aResps)

    LOCAL oSection1   := oReport:SECTION(1)
    LOCAL oSection2   := oReport:SECTION(2)
    LOCAL oSection3   := oReport:SECTION(3)
    LOCAL cQuery      := ""
    LOCAL cQuery1     := ""
    LOCAL cQuery2     := ""
    LOCAL aArea       := GETAREA()
    LOCAL oStatement

    IF LEN(aResps) == 3
        cResult    := aResps[1]
        aPedidoDE  := aResps[2]
        aPedidoATE := aResps[3]
    ELSEIF LEN(aResps) == 2
        cResult    := ""
        aPedidoDE  := aResps[1]
        aPedidoATE := aResps[2]
    ENDIF

    IF cResult == "AGUARDANDO IMPRESSÃO"
       cResult := "AX" 
    ENDIF

    FWDIRREMOVE("C:\imagem do pedido",.T.,.T.)

      
    cQuery := " SELECT  CASE " + CRLF
    cQuery += " WHEN A.[C5_TIPO] = 'B' THEN A.[C5_CLIENTE] " + CRLF
    cQuery += " ELSE B.[A1_COD] " + CRLF
	cQuery += "	END AS [CODIGO_CLIENTE], " + CRLF
	cQuery += "	CASE " + CRLF
    cQuery += " WHEN A.[C5_TIPO] = 'B' THEN G.[A2_LOJA] " + CRLF
    cQuery += " ELSE B.[A1_LOJA] " + CRLF
	cQuery += "	END AS [LOJA], " + CRLF
	cQuery += "	CASE " + CRLF
    cQuery += " WHEN A.[C5_TIPO] = 'B' THEN G.[A2_NREDUZ] " + CRLF
    cQuery += " ELSE B.[A1_NREDUZ] " + CRLF
	cQuery += "	END AS [NOME], " + CRLF
	cQuery += " CASE " + CRLF 
    cQuery += " WHEN A.[C5_TIPO] = 'B' THEN TRIM(G.[A2_END]) + (' ' + '-' + ' ' + G.[A2_EST]) " + CRLF
    cQuery += " ELSE TRIM(B.[A1_END]) + (' ' + '-' + ' ' + B.[A1_EST]) " + CRLF
	cQuery += "	END AS [ENDERECO], " + CRLF
	cQuery += "	A.[C5_NUM], " + CRLF
	cQuery += "	FORMAT(CONVERT(DATE, A.[C5_EMISSAO]), 'dd/MM/yy') AS [EMISSAO], " + CRLF
	cQuery += "	C.[A4_NOME], " + CRLF
	cQuery += "	D.[E4_DESCRI], " + CRLF
	cQuery += "	E.[A3_NOME], " + CRLF
	cQuery += "	MAX(F.[F4_ESTOQUE]) AS CONTROLA_ESTOQUE, " + CRLF
	cQuery += "	A.[C5_PEDMER] " + CRLF
    cQuery += " FROM " + RETSQLNAME("SC5") + " A " + CRLF
    cQuery += " LEFT JOIN " + RETSQLNAME("SA1") + " B " + CRLF
    cQuery += " ON A.[C5_CLIENTE] = B.[A1_COD] " + CRLF
    cQuery += " LEFT JOIN " + RETSQLNAME("SA4") + " C " + CRLF
    cQuery += " ON A.[C5_TRANSP] = C.[A4_COD] " + CRLF
    cQuery += " LEFT JOIN " + RETSQLNAME("SE4") + " D " + CRLF
    cQuery += " ON A.[C5_CONDPAG] = D.[E4_CODIGO] " + CRLF
    cQuery += " LEFT JOIN " + RETSQLNAME("SA3") + " E " + CRLF
    cQuery += " ON A.[C5_VEND1] = E.[A3_COD] " + CRLF 
    cQuery += " LEFT JOIN (SELECT A.[C6_NUM], A.[C6_TES], A.[C6_LOCAL], B.[F4_ESTOQUE] FROM " + RETSQLNAME("SC6") + " A " + CRLF
    cQuery += " LEFT JOIN " + RETSQLNAME("SF4") + " B " + CRLF
    cQuery += " ON A.[C6_TES] = B.[F4_CODIGO]) AS F " + CRLF
    cQuery += " ON A.[C5_NUM] = F.[C6_NUM] " + CRLF
    cQuery += " LEFT JOIN " + RETSQLNAME("SA2") + " G " + CRLF
    cQuery += " ON A.[C5_CLIENTE] = G.[A2_COD] " + CRLF
    IF !EMPTY(cResult)
        cQuery += " WHERE A.[D_E_L_E_T_] = ' ' AND A.[C5_XSTEX] = '"+cResult+"' AND F.[F4_ESTOQUE] <> 'N' AND F.[C6_LOCAL] = '006' AND " + CRLF
        cQuery += " CASE " + CRLF
        cQuery += " WHEN B.A1_LOJA = '01' THEN B.A1_LOJA " + CRLF
        cQuery += " WHEN G.A2_LOJA = '01' THEN G.A2_LOJA " + CRLF
        cQuery += " END = 01 " + CRLF
    ELSE
        cQuery += " WHERE A.[D_E_L_E_T_] = ' ' AND A.[C5_NUM] BETWEEN '"+aPedidoDE+"' AND '"+aPedidoATE+"' AND F.[F4_ESTOQUE] <> 'N' AND F.[C6_LOCAL] = '006' AND " + CRLF
        cQuery += " CASE " + CRLF
        cQuery += " WHEN B.A1_LOJA = '01' THEN B.A1_LOJA " + CRLF
        cQuery += " WHEN G.A2_LOJA = '01' THEN G.A2_LOJA " + CRLF
        cQuery += " END = 01 " + CRLF
    ENDIF
    cQuery += " GROUP BY A.[C5_CLIENTE], " + CRLF
    cQuery += " A.[C5_NUM], " + CRLF
    cQuery += " B.[A1_COD], " + CRLF
	cQuery += "	B.[A1_LOJA], " + CRLF
	cQuery += "	B.[A1_NREDUZ], " + CRLF
	cQuery += "	B.[A1_EST], " + CRLF
	cQuery += "	B.[A1_END], " + CRLF
	cQuery += "	A.[C5_TIPO], " + CRLF
	cQuery += "	A.[C5_EMISSAO], " + CRLF
	cQuery += "	C.[A4_NOME], " + CRLF
	cQuery += "	D.[E4_DESCRI], " + CRLF
	cQuery += "	E.[A3_NOME], " + CRLF
	cQuery += "	F.[F4_ESTOQUE], " + CRLF
	cQuery += "	A.[C5_PEDMER], " + CRLF
	cQuery += "	G.[A2_LOJA], " + CRLF
	cQuery += "	G.[A2_END], " + CRLF
	cQuery += "	G.[A2_NREDUZ], " + CRLF
	cQuery += "	G.[A2_EST] " + CRLF
    cQuery += " ORDER BY A.[C5_NUM] "

    cAliasCL := MPSYSOPENQUERY(cQuery)

        WHILE (cAliasCL)->(!EOF())
            oSection1:INIT()
            oSection1:PRINTLINE()
            oSection1:SETPAGEBREAK(.T.)

            FWMAKEDIR("c:\imagem do pedido")

            url  := "https://barcode.tec-it.com/barcode.ashx?data=" + (cAliasCL)->C5_NUM
            Html := HTTPGET(url)
            Html := MEMOWRITE(ALLTRIM("C:\imagem do pedido\barcode"+ (cAliasCL)->C5_NUM +".png"),Html)
            oReport:SAYBITMAP(50,1700,"C:\imagem do pedido\barcode"+ (cAliasCL)->C5_NUM +".png",150,150)

                cQuery1 := " SELECT ROUND(COALESCE(B.[C6_QTDVEN] / NULLIF(C.[B1_CONV],0), 0), 1) AS [EMBAL], " + CRLF
                cQuery1 += " CASE WHEN B.[C6_BLQ] = 'R' THEN CONCAT(COALESCE(B.[C6_QTDVEN] - B.[C6_QTDENT],''), ' ', COALESCE('C','')) " + CRLF
                cQuery1 += " ELSE REPLACE(CONVERT(VARCHAR, B.[C6_QTDVEN]), '', '') " + CRLF
                cQuery1 += " END AS [C6_QTDVEN], " + CRLF
                cQuery1 += " B.[C6_PRODUTO], " + CRLF
                cQuery1 += " B.[C6_DESCRI], " + CRLF 
                cQuery1 += " B.[C6_PRCVEN], " + CRLF
                cQuery1 += " B.[C6_VALOR], " + CRLF
                cQuery1 += " B.[C6_ENDPAD], " + CRLF
                cQuery1 += " B.[C6_NUM], " + CRLF
                cQuery1 += " CASE WHEN B.[C6_BLQ] = 'R' THEN B.[C6_QTDEMP] * B.[C6_PRCVEN] ELSE B.[C6_QTDVEN] * B.[C6_PRCVEN]  END AS [VALOR_EXP] " + CRLF
	            cQuery1 += " FROM " + RETSQLNAME("SC5") + " A " + CRLF
                cQuery1 += " LEFT JOIN " + RETSQLNAME("SC6") + " B " + CRLF 
                cQuery1 += " ON A.[C5_NUM] = B.[C6_NUM] " + CRLF
                cQuery1 += " LEFT JOIN " + RETSQLNAME("SB1") + " C " + CRLF
                cQuery1 += " ON B.[C6_PRODUTO] = C.[B1_COD] " + CRLF
                cQuery1 += " LEFT JOIN " + RETSQLNAME("SA1") + " D " + CRLF
                cQuery1 += " ON B.[C6_CLI] = D.[A1_COD] " + CRLF
                IF !EMPTY(cResult)
                    cQuery1 += " WHERE A.[D_E_L_E_T_] = ' ' AND A.[C5_XSTEX] = '" + cResult +"'" + CRLF
                ELSE
                    cQuery1 += " WHERE A.[D_E_L_E_T_] = ' ' AND A.[C5_NUM] BETWEEN '"+ aPedidoDE +"' AND '"+ aPedidoATE +"'" + CRLF
                ENDIF        
                cQuery1 += " ORDER BY A.[C5_NUM] "
                                                     
                cAliasPD := MPSYSOPENQUERY(cQuery1)

                    WHILE (cAliasPD)->(!EOF())
                        IF (cAliasCL)->C5_NUM == (cAliasPD)->C6_NUM 
                            oSection2:INIT()
                            oSection2:PRINTLINE()
                        ENDIF
                        (cAliasPD)->(DBSKIP())
                    ENDDO

                        DBSELECTAREA("SC5")
                        DBSETORDER(1)
                        DBSEEK(XFILIAL("SC5")+(cAliasCL)->C5_NUM)
                            IF FOUND()
                                OBS := u_LerMemoA(SC5->C5_XOBS)   
                            ENDIF
                        RESTAREA(aArea)

                        oStatement := FWPREPAREDSTATEMENT():NEW()

                        cQuery2 := " SELECT ? AS C5_XOBS, " + CRLF
                        cQuery2 += " A.[C5_NUM] " + CRLF
                     	cQuery2 += " FROM " + RETSQLNAME("SC5") + " A " + CRLF
                        cQuery2 += " LEFT JOIN " + RETSQLNAME("SA1") + " B " + CRLF 
                        cQuery2 += " ON A.[C5_CLIENTE] = B.[A1_COD] " + CRLF
                        IF !EMPTY(cResult)
                            cQuery2 += " WHERE A.[D_E_L_E_T_] = ' ' AND A.[C5_XSTEX] = '" + cResult +"'" + CRLF
                        ELSE
                            cQuery2 += " WHERE A.[D_E_L_E_T_] = ' ' AND A.[C5_NUM] BETWEEN '"+ aPedidoDE +"' AND '"+ aPedidoATE +"'" + CRLF
                        ENDIF        
                        cQuery2 += " ORDER BY A.[C5_NUM]"

                        oStatement:SETQUERY(cQuery2)
                        oStatement:SETSTRING(1,OBS)                                  
                        cQuery2 := oStatement:GETFIXQUERY()

                        cAliasOB := MPSYSOPENQUERY(cQuery2)

                            WHILE (cAliasOB)->(!EOF())
                                IF (cAliasCL)->C5_NUM == (cAliasOB)->C5_NUM
                                    oSection3:INIT()
                                    oSection3:PRINTLINE()
                                ENDIF        
                                (cAliasOB)->(DBSKIP())
                            ENDDO

                            DBSELECTAREA("SC5")
                            DBSETORDER(1)
                            DBSEEK(XFILIAL("SC5")+(cAliasCL)->C5_NUM)
                                IF FOUND()
                                    IF SC5->C5_XSTEX == 'AX' .AND. cResult == "AX"
                                        RECLOCK("SC5", .F.)
                                            SC5->C5_XSTEX := 'AL'
                                        MSUNLOCK()
                                    ELSEIF SC5->C5_XSTEX == 'AX' .AND. cResult == ""
                                        RECLOCK("SC5", .F.)
                                            SC5->C5_XSTEX := 'AX'
                                        MSUNLOCK()  
                                    ENDIF
                                ENDIF
                            RESTAREA(aArea)
                            
                            oSection3:FINISH()
                            (cAliasOB)->(DBCLOSEAREA()) 

                    oReport:SKIPLINE(1)
                    oSection2:FINISH()
                    (cAliasPD)->(DBCLOSEAREA())  

            (cAliasCL)->(DBSKIP())
            oSection1:FINISH()

        ENDDO

    (cAliasCL)->(DBCLOSEAREA())

RETURN
